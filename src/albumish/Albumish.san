/*
 *  Copyright (c) 2014  Salvatore Valente <svalente@mit.edu>
 *
 *  This program is free software.  You can modify and distribute it under
 *  the terms of the GNU General Public License.  There is no warranty.
 *  See the file "COPYING" for more information.
 */
package albumish;
import sanka.io.File;
import gtk.Application;
import gtk.Grid;
import gtk.ScrolledWindow;
import gtk.Window;
import albumish.database.CheckDatabase;
import albumish.database.Database;
import albumish.database.Playlist;
import albumish.database.Song;
import albumish.filter.FilterWorker;

class Albumish {

const AUTO_PLAYLIST = 0;

Database database;
CheckDatabase checkDatabase;
FilterWorker filterWorker;
Playlist[] playlists;
PlayerThread playerThread;
TopPanel topPanel;
CoverPanel coverPanel;
SongPanel playlistSongPanel;
SongPanel albumSongPanel;

static void main(String[] argv) {
    new Albumish().run(argv);
}

void run(String[] argv)
{
    var app = new Application(null, 0);
    app.connect("activate", this);
    app.run(argv);
    app.unref();
}

void callback(Application app) {
    var home = new File(System.getenv("HOME"));
    var directory = new File(home, ".albumish");
    this.database = new Database();
    this.database.load(directory, "database.json");
    this.checkDatabase = new CheckDatabase(this.database, directory, "checked_songs.list");
    // this.config = new Configuration(directory, "config.json");
    // this.playlists = new PlaylistCollection(this.database, directory);
    this.playlists = {new Playlist()};
    // this.num_random_playlists = this.playlists.get_max_with_prefix(RANDOM_PLAYLIST_PREFIX);
    // directory = new File(home, "Pictures/covers");
    // this.gallery = new Gallery(this, directory, COVER_SIZES);
    this.filterWorker = new FilterWorker(this.database); // , this.check_database);
    this.playerThread = new PlayerThread(this);

    var window = new Window(app);
    window.setTitle("Albumish");
    // window.setImage(get_icon("CD.png"));
    window.setBorderWidth(10);
    var grid = new Grid();
    window.add(grid);

    // makeMenuBar(this.main_window);
    this.topPanel = new TopPanel(this);
    grid.attach(this.topPanel.widget(), 0, 0, 3, 1);
    var widget = null;
    var scrolledWindow = null;

    this.coverPanel = new CoverPanel(this);
    widget = this.coverPanel.widget();
    widget.setExpand(true, true);
    scrolledWindow = new ScrolledWindow();
    scrolledWindow.setPolicy(ScrolledWindow.POLICY_AUTOMATIC, ScrolledWindow.POLICY_AUTOMATIC);
    scrolledWindow.setShadowType(ScrolledWindow.SHADOW_OUT);
    scrolledWindow.add(widget);
    grid.attach(scrolledWindow, 0, 1, 3, 1);

    var sortPanel = new SortPanel(this);
    widget = sortPanel.widget();
    widget.setExpand(false, false);
    grid.attach(widget, 0, 2, 1, 1);

    this.playlistSongPanel = new SongPanel(this, false);
    widget = this.playlistSongPanel.widget();
    widget.setExpand(true, true);
    scrolledWindow = new ScrolledWindow();
    scrolledWindow.setPolicy(ScrolledWindow.POLICY_AUTOMATIC, ScrolledWindow.POLICY_AUTOMATIC);
    scrolledWindow.setShadowType(ScrolledWindow.SHADOW_OUT);
    scrolledWindow.add(widget);
    grid.attach(scrolledWindow, 1, 2, 1, 2);

    this.albumSongPanel = new SongPanel(this, true);
    widget = this.albumSongPanel.widget();
    widget.setExpand(true, true);
    scrolledWindow = new ScrolledWindow();
    scrolledWindow.setPolicy(ScrolledWindow.POLICY_AUTOMATIC, ScrolledWindow.POLICY_AUTOMATIC);
    scrolledWindow.setShadowType(ScrolledWindow.SHADOW_OUT);
    scrolledWindow.add(widget);
    grid.attach(scrolledWindow, 2, 2, 1, 2);

    // this.playlist_panel = new PlaylistPanel(this, this.playlists);
    // data = new GridData(SWT.FILL, SWT.FILL, false, true);
    // data.heightHint = bounds.height / 5;
    // this.playlist_panel.getControl().setLayoutData(data);

    var albumList = this.filterWorker.generate_album_list();
    this.coverPanel.reset(albumList);
    window.setSizeRequest(1000, 500);
    window.showAll();
}

/**
 * Called by cover_panel. Update song_panel and album_song_panel.
 */
void selectAlbum(int albumid) {
    if (albumid >= 0) {
        this.albumSongPanel.updateDatabase();
        var songList = this.filterWorker.generate_song_list(albumid);
        this.albumSongPanel.reset(-1, songList);
    }
}

/**
 * Called by sort_panel. Update cover_panel.
 */
void sortAlbums(int sortType) {
    this.coverPanel.sort(sortType);
}

/**
 * Called by song_panel. Update playlist panels.
 */
int setAutoPlaylist(int[] songList) {
    var playlistid = AUTO_PLAYLIST;
    this.playlists[playlistid].reset(songList);
    // TODO this.playlistPanel.set...
    this.playlistSongPanel.reset(playlistid, songList);
    return playlistid;
}

/**
 * Called by top_panel.
 */
void doPrevious() {
    // If you press Previous in the first 3 seconds of a song, then go to the previous song.
    // Otherwise, restart the current song.
    var delta = this.playerThread.get_audio_position_in_ms() < 3000.0 ? -1 : 0;
    this.playerThread.play_next_song(delta);
}

/**
 * Called by top_panel.
 */
void doNext() {
    this.playerThread.play_next_song(1);
}

/**
 * Called by top_panel.
 */
void playOrPause() {
    if this.playerThread.is_paused {
        this.playerThread.pause(false);
        this.topPanel.displayPauseButton();
        return;
    }
    if this.playerThread.is_playing() {
        this.playerThread.pause(true);
        this.topPanel.displayPlayButton();
        return;
    }
    var playlistid = this.playlistSongPanel.playlistid;
    if playlistid >= 0 {
        var songid = this.playlistSongPanel.getSelectedId();
        if songid > 0 {
            playSong(playlistid, songid);
        }
    }
}

/**
 * Called by playlistSongPanel and albumSongPanel.
 */
void playSong(int playlistid, int songid) {
    this.playerThread.play_songid(playlistid, songid);
}

/**
 * Called by player_thread.
 */
void update_top_panel(Song song) {
    if song == null {
        this.topPanel.display(null, null, null, 0);
        return;
    }
    var artist_name = null;
    if song.artistid > 0 {
        artist_name = this.database.artist_list[song.artistid].name;
    }
    var album_name = null;
    if song.albumid > 0 {
        album_name = this.database.album_list[song.albumid].name;
    }
    this.topPanel.display(song.title, artist_name, album_name, song.duration);
}

void display_time(int millis) {
    this.topPanel.display_time(millis);
}
}
