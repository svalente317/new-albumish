/*
 *  Copyright (c) 2014  Salvatore Valente <svalente@mit.edu>
 *
 *  This program is free software.  You can modify and distribute it under
 *  the terms of the GNU General Public License.  There is no warranty.
 *  See the file "COPYING" for more information.
 */
package albumish;
import sanka.io.File;
import gtk.Application;
import gtk.Grid;
import gtk.MenuBar;
import gtk.MenuItem;
import gtk.ScrolledWindow;
import gtk.Window;
import albumish.database.CheckDatabase;
import albumish.database.Database;
import albumish.database.Playlist;
import albumish.database.Song;
import albumish.filter.FilterWorker;
import albumish.sync.SyncManager;

class Albumish {

// Draw unselected albums in a square of 225x225.
// Draw the selected album in a square of 300x300.
const COVER_SIZE = 225;
const SELECTED_COVER_SIZE = 300;

const AUTO_PLAYLIST = 0;

const ADD_FOLDER = 1;
const RIP_CD = 2;
const SYNC_TO_DEVICE = 3;
const QUIT = 4;
const PREV_ALBUM = 5;
const NEXT_ALBUM = 6;
const JUMP_ALBUM = 7;
const LOAD_ALBUM_ART = 8;
const NEW_PLAYLIST = 9;
const DELETE_PLAYLIST = 10;
const ADD_SELECTION = 11;
const DELETE_SELECTION = 12;
const RANDOM_PLAYLIST = 13;

map[String]String config;
File home_directory;
Database database;
CheckDatabase check_database;
Gallery gallery;
FilterWorker filter_worker;
Playlist[] playlists;
PlayerThread player_thread;
Window window;
TopPanel top_panel;
CoverPanel cover_panel;
SongPanel playlist_song_panel;
SongPanel album_song_panel;
PlaylistPanel playlist_panel;

static void main(String[] argv) {
    new Albumish().run(argv);
}

void run(String[] argv)
{
    var app = new Application(null, 0);
    app.connect("activate", this);
    app.run(argv);
    app.unref();
}

void callback(Application app) {
    this.config = new map[String]String;
    this.home_directory = new File(System.getenv("HOME"));
    var directory = new File(this.home_directory, ".albumish");
    this.database = new Database();
    this.database.load(directory, "database.json");
    this.check_database = new CheckDatabase(this.database, directory, "checked_songs.list");
    // this.config = new Configuration(directory, "config.json");
    // this.playlists = new PlaylistCollection(this.database, directory);
    this.playlists = {new Playlist()};
    // this.num_random_playlists = this.playlists.get_max_with_prefix(RANDOM_PLAYLIST_PREFIX);
    directory = new File(this.home_directory, "Pictures/covers");
    this.gallery = new Gallery(this, directory, COVER_SIZE, SELECTED_COVER_SIZE);
    this.filter_worker = new FilterWorker(this.database); // , this.check_database);
    this.player_thread = new PlayerThread(this);

    this.window = new Window(app);
    this.window.set_title("Albumish");
    // window.setImage(get_icon("CD.png"));
    var grid = new Grid();
    grid.set_border_width(10);
    grid.set_row_spacing(10);
    grid.set_column_spacing(10);
    this.window.add(grid);

    makeMenuBar(app);
    this.top_panel = new TopPanel(this);
    grid.attach(this.top_panel.widget(), 0, 0, 3, 1);
    var widget = null;
    var scrolledWindow = null;

    this.cover_panel = new CoverPanel(this);
    widget = this.cover_panel.widget();
    grid.attach(widget, 0, 1, 3, 1);

    var sortPanel = new SortPanel(this);
    widget = sortPanel.widget();
    widget.set_expand(false, false);
    grid.attach(widget, 0, 2, 1, 1);

    this.playlist_song_panel = new SongPanel(this, false);
    widget = this.playlist_song_panel.widget();
    widget.set_expand(true, true);
    scrolledWindow = new ScrolledWindow();
    scrolledWindow.set_policy(ScrolledWindow.POLICY_AUTOMATIC, ScrolledWindow.POLICY_AUTOMATIC);
    scrolledWindow.set_shadow_type(ScrolledWindow.SHADOW_OUT);
    scrolledWindow.add(widget);
    grid.attach(scrolledWindow, 1, 2, 1, 2);

    this.album_song_panel = new SongPanel(this, true);
    widget = this.album_song_panel.widget();
    widget.set_expand(true, true);
    scrolledWindow = new ScrolledWindow();
    scrolledWindow.set_policy(ScrolledWindow.POLICY_AUTOMATIC, ScrolledWindow.POLICY_AUTOMATIC);
    scrolledWindow.set_shadow_type(ScrolledWindow.SHADOW_OUT);
    scrolledWindow.add(widget);
    grid.attach(scrolledWindow, 2, 2, 1, 2);

    this.playlist_panel = new PlaylistPanel(this);
    widget = this.playlist_panel.widget();
    widget.set_expand(false, true);
    scrolledWindow = new ScrolledWindow();
    scrolledWindow.set_policy(ScrolledWindow.POLICY_AUTOMATIC, ScrolledWindow.POLICY_AUTOMATIC);
    scrolledWindow.set_shadow_type(ScrolledWindow.SHADOW_OUT);
    scrolledWindow.set_size_request(200, 150);
    scrolledWindow.add(widget);
    grid.attach(scrolledWindow, 0, 3, 1, 1);

    var albumList = this.filter_worker.generate_album_list();
    this.cover_panel.reset(albumList);
    this.window.set_size_request(1500, 500);
    this.window.show_all();
}

private void makeMenuBar(Application app) {
    var menubar = new MenuBar(app, this);
    menubar.add_menu("Library", {
        new MenuItem(){label: "_Add Folder to Library...", accel: "<Primary>a", item_id: ADD_FOLDER},
        new MenuItem(){label: "_Rip CD to Library...", accel: null, item_id: RIP_CD},
        new MenuItem(){label: "_Sync to Device...", accel: null, item_id: SYNC_TO_DEVICE},
        new MenuItem(){label: "_Quit", accel: null, item_id: QUIT}
    });
    menubar.add_menu("Albums", {
        new MenuItem(){label: "_Previous Album", accel: "Page_Up", item_id: PREV_ALBUM},
        new MenuItem(){label: "_Next Album", accel: "Page_Down", item_id: NEXT_ALBUM},
        new MenuItem(){label: "_Jump to Playing Album", accel: "<Primary>j", item_id: JUMP_ALBUM},
        new MenuItem(){label: "Load Album Art...", accel: null, item_id: LOAD_ALBUM_ART}
    });
    menubar.install();
}

/**
 * Called by cover_panel. Update song_panel and album_song_panel.
 */
void select_album(int albumid) {
    if albumid >= 0 {
        this.album_song_panel.update_database();
        var songList = this.filter_worker.generate_song_list(albumid);
        this.album_song_panel.reset(-1, songList);
    }
}

/**
 * Called by sort_panel. Update cover_panel.
 */
void sort_albums(int sort_type) {
    this.cover_panel.sort(sort_type);
}

/**
 * Called by song_panel. Update playlist panels.
 */
int set_auto_playlist(int[] songList) {
    var playlistid = AUTO_PLAYLIST;
    this.playlists[playlistid].reset(songList);
    // TODO this.playlistPanel.set...
    this.playlist_song_panel.reset(playlistid, songList);
    return playlistid;
}

/**
 * Called by top_panel.
 */
void do_previous() {
    // If you press Previous in the first 3 seconds of a song, then go to the previous song.
    // Otherwise, restart the current song.
    var delta = this.player_thread.get_audio_position_in_ms() < 3000.0 ? -1 : 0;
    this.player_thread.play_next_song(delta);
}

/**
 * Called by top_panel.
 */
void do_next() {
    this.player_thread.play_next_song(1);
}

/**
 * Called by top_panel.
 */
void play_or_pause() {
    if this.player_thread.is_paused {
        this.player_thread.pause(false);
        this.top_panel.display_pause_button();
        return;
    }
    if this.player_thread.is_playing() {
        this.player_thread.pause(true);
        this.top_panel.display_play_button();
        return;
    }
    var playlistid = this.playlist_song_panel.playlistid;
    if playlistid >= 0 {
        var songid = this.playlist_song_panel.get_selected_id();
        if songid > 0 {
            play_song(playlistid, songid);
        }
    }
}

/**
 * Called by playlist_song_panel and album_song_panel.
 */
void play_song(int playlistid, int songid) {
    this.player_thread.play_songid(playlistid, songid);
}

/**
 * Called by player_thread.
 */
void update_top_panel(Song song) {
    if song == null {
        this.top_panel.display(null, null, null, 0);
        return;
    }
    var artist_name = null;
    if song.artistid > 0 {
        artist_name = this.database.artist_list[song.artistid].name;
    }
    var album_name = null;
    if song.albumid > 0 {
        album_name = this.database.album_list[song.albumid].name;
    }
    this.top_panel.display(song.title, artist_name, album_name, song.duration);
}

void display_time(int millis) {
    this.top_panel.display_time(millis);
}

/**
 * Called by top_panel. Update cover_panel.
 */
void set_filter(String text) {
    var album_list = null;
    if text == null || text == "" {
        album_list = this.filter_worker.clear();
    } else {
        album_list = this.filter_worker.set_filter(text);
    }
    this.cover_panel.reset(album_list);
}

/**
 * Called by gallery. Update cover_panel.
 */
void invalidate_album(int albumid) {
    this.cover_panel.invalidate_album(albumid);
}

File get_file(Song song) {
    return new File(this.home_directory, song.filename);
}

void menu_activated(int item_id) {
    switch item_id {
    case ADD_FOLDER:
        break;
    case RIP_CD:
        break;
    case SYNC_TO_DEVICE:
        new SyncManager().sync_to_device(this);
        break;
    case QUIT:
        this.database.save();
        this.check_database.save();
        this.window.close();
        break;
    case PREV_ALBUM:
        this.cover_panel.select_next_album(-1);
        break;
    case NEXT_ALBUM:
        this.cover_panel.select_next_album(1);
        break;
    case JUMP_ALBUM:
        break;
    case LOAD_ALBUM_ART:
        break;
    }
}
}
