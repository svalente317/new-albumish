package albumish;
import sanka.json.JsonElement;
import gtk.CellRenderer;
import gtk.ListStore;
import gtk.TreeView;
import gtk.TreeSelection;
import gtk.TreeViewColumn;
import gtk.Widget;
import albumish.filter.AlbumSorter;

class CoverPanel {

private Albumish player;
private AlbumSorter sorter;
private TreeView treeView;
private ListStore listStore;

CoverPanel(Albumish player) {
    this.player = player;
    this.sorter = new AlbumSorter(player.database);
    this.treeView = new TreeView();
    var renderer = CellRenderer.text();
    var column = new TreeViewColumn("Album", renderer, true, "text", 0);
    this.treeView.appendColumn(column);
    this.listStore = new ListStore({ListStore.TYPE_STRING, ListStore.TYPE_INT});
    this.treeView.setModel(this.listStore);
    // g_object_unref(list_store);

    var treeSelection = this.treeView.getSelection();
    treeSelection.connect("changed", this);
}

Widget widget() {
    return this.treeView;
}

void reset(int[] albumList) {
    this.sorter.sort(albumList);
    var row = {new JsonElement(), new JsonElement()};
    var iter = new TreeIter();
    var valid = this.listStore.getIterFirst(iter);
    for var albumid : albumList {
        if !valid {
            this.listStore.append(iter);
        }
        var album = this.player.database.album_list[albumid];
        row[0].makeString(album.name);
        row[1].makeInt(albumid);
        this.listStore.set(iter, row);
        valid = this.listStore.iterNext(iter);
    }
}

void sort(int sortType) {
    // TODO var albumid = get_selected_albumid();
    this.sorter.set_sort_type(sortType);
    var albumList = new int[];
    var iter = new TreeIter();
    var value = new JsonElement();
    for var ok = this.listStore.getIterFirst(iter); ok; ok = this.listStore.iterNext(iter) {
        this.listStore.getValue(iter, 1, value);
        albumList.add(value.getAsInt());
    }
    reset(albumList);
    // TODO select albumid
}

void callback(TreeSelection selection) {
    var iter = new TreeIter();
    if !selection.getSelected(iter) {
        return;
    }
    var value = new JsonElement();
    this.listStore.getValue(iter, 1, value);
    var albumid = value.getAsInt();
    this.player.selectAlbum(albumid);
}
}
