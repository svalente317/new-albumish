/*
 *  Copyright (c) 2014  Salvatore Valente <svalente@mit.edu>
 *
 *  This program is free software.  You can modify and distribute it under
 *  the terms of the GNU General Public License.  There is no warranty.
 *  See the file "COPYING" for more information.
 */
package albumish;
import sanka.json.JsonElement;
import gtk.CellRenderer;
import gtk.ListStore;
import gtk.TreeIter;
import gtk.TreeView;
import gtk.TreeSelection;
import gtk.TreeViewColumn;
import gtk.Widget;
import albumish.database.PlaylistCollection;

class PlaylistPanel {

    private Albumish player;
    private TreeView tree_view;
    private ListStore list_store;

    PlaylistPanel(Albumish player) {
        this.player = player;
        this.tree_view = new TreeView();
        var renderer = CellRenderer.text();
        var column = new TreeViewColumn("Playlist", renderer, true, "text", 0);
        this.tree_view.append_column(column);
        this.list_store = new ListStore({ListStore.TYPE_STRING, ListStore.TYPE_INT});
        this.tree_view.set_model(this.list_store);
        // g_object_unref(list_store);

        var row = {new JsonElement(), new JsonElement()};
        var iter = new TreeIter();
        for var playlist : player.playlists {
            this.list_store.append(iter);
            row[0].makeString(playlist.name);
            row[1].makeInt(playlist.id);
            this.list_store.set(iter, row);
        }
        this.tree_view.get_selection().connect("changed", this);
        select(PlaylistCollection.AUTO_PLAYLIST);
    }

    Widget widget() {
        return this.tree_view;
    }

    /**
     * callback indicates that the user has clicked a new playlist.
     * Tell the player to display the playlist in the PlaylistSongPanel.
     */
    void callback(TreeSelection selection) {
        var iter = new TreeIter();
        if !selection.get_selected(iter) {
            return;
        }
        var value = new JsonElement();
        this.list_store.get_value(iter, 1, value);
        var playlistid = value.getAsInt();
        this.player.select_playlist(playlistid);
    }

    /**
     * select indicates that the user wants to view or play a new playlist.
     * Update only the playlist panel. Do not trigger further changes.
     */
    void select(int targetid) {
        var iter = new TreeIter();
        var value = new JsonElement();
        for var ok = this.list_store.get_iter_first(iter); ok; ok = this.list_store.iter_next(iter) {
            this.list_store.get_value(iter, 1, value);
            if value.getAsInt() == targetid {
                this.tree_view.get_selection().select(iter);
            }
        }
    }
}
