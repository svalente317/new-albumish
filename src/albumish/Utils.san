/*
 *  Copyright (c) 2014  Salvatore Valente <svalente@mit.edu>
 *
 *  This program is free software.  You can modify and distribute it under
 *  the terms of the GNU General Public License.  There is no warranty.
 *  See the file "COPYING" for more information.
 */
package albumish;
import sanka.io.File;
import sanka.io.FileStats;
import sanka.util.StringBuilder;

class Utils {
    const BAD_CHARS = "/:?\"";

    /**
     * For each bad character --
     * If it appears with no space on either side of it, then replace it with a space.
     * If it appears with a space on one side of it, then remove it.
     * If it appears with a space on both sides of it, then remove it and the next space.
     */
    static String name_to_filename(String name) {
        var filename = new StringBuilder(name.length());
        var haveSpace = true;
        var needSpace = false;
        var skipSpace = false;
        var length = name.length();
        for var idx = 0; idx < length; idx++ {
            var ch = name[idx];
            if BAD_CHARS.indexOf(ch) >= 0 {
                needSpace = !haveSpace;
                haveSpace = false;
                skipSpace = true;
            } else {
                if needSpace && ch != '.' {
                    filename.add(' ');
                    needSpace = false;
                }
                if skipSpace {
                    skipSpace = false;
                    if ch == ' ' {
                        continue;
                    }
                }
                filename.add(ch);
                haveSpace = (ch == ' ');
            }
        }
        return filename.done();
    }

    /**
     * In vfat, a directory name can not end with a dot.
     */
    static String name_to_dirname(String name) {
        name = name_to_filename(name);
        var idx = name.length() - 1;
        if name[idx] == '.' {
            name = name.substring(0, idx) + "_";
        }
        return name;
    }

    static String[] read_directory_tree(File topdir, map[String]boolean known_files) {
        var results = new String[];
        var workqueue = new File[];
        workqueue.add(topdir);
        var stats = new FileStats();
        while workqueue.length > 0 {
            var directory = workqueue[0];
            workqueue.delete(0, 1);
            var names = directory.list();
            if names == null {
                continue;
            }
            for var name : names {
                var file = new File(directory, name);
                var status = file.getStats(stats, true);
                if status != 0 {
                    System.println(file.getPath() + ": " + System.strerror(status));
                    continue;
                }
                if stats.isDirectory {
                    workqueue.add(file);
                } else if stats.isFile {
                    var pathname = file.getPath();
                    if !known_files[pathname] {
                        results.add(pathname);
                    }
                } else {
                    System.println(file.getPath() + ": not a file or directory");
                }
            }
        }
        return results;
    }
}
