/*
 *  Copyright (c) 2014  Salvatore Valente <svalente@mit.edu>
 *
 *  This program is free software.  You can modify and distribute it under
 *  the terms of the GNU General Public License.  There is no warranty.
 *  See the file "COPYING" for more information.
 */
package albumish.sync;
import sanka.io.File;
import sanka.io.FileStats;
import albumish.Albumish;
import albumish.Utils;
import albumish.database.Song;
import albumish.database.SongInfo;
import albumish.dialogs.InputDialog;
import albumish.dialogs.ProgressDialog;
import audio.FrameInfo;
import audio.Mpg123;

class AddToLibraryManager {

    Albumish player;
    private String pathname;
    private ProgressDialog dialog;
    private int file_count;
    private int done_count;
    private Mpg123 mpg;

    /**
     * Pop up an input dialog to select a folder to add.
     */
    void add_to_library(Albumish player) {
        this.player = player;
        var title = "Add Folder to Library";
        var prompt = "Enter Folder to Add to Library.";
        new InputDialog(player.window, title, prompt, null, this);
    }

    /**
     * Pop up a progress dialog, and start a background thread to add the folder.
     */
    void callback(String pathname) {
        this.pathname = pathname;
        this.dialog = new ProgressDialog(this.player.window,
                "Add to Library...", "Processing " + this.pathname + "...");
        new Thread(this);
    }

    /**
     * Find every file in the directory tree rooted at the given pathname. Add each media file to
     * the database.
     */
    void run() {
        var database = this.player.database;
        var known_files = new map[String]boolean;
        for var song : database.song_list {
            if song != null && song.filename != null {
                var pathname = this.player.get_file(song).getPath();
                known_files[pathname] = true;
            }
        }
        var file_list = Utils.read_directory_tree(new File(this.pathname), known_files);
        this.file_count = file_list.length;
        this.done_count = 0;
        this.dialog.set_bottom_label("Done: 0 / " + this.file_count);
        this.mpg = new Mpg123();
        for var filename : file_list {
            if this.pathname == null {
                break;
            }
            var song = new Song();
            var obj = new SongInfo();
            read_id3_tags(filename, song, obj);
            if song.title != null {
                database.add_song(song, obj);
            }
            this.done_count++;
            this.dialog.set_progress(this.file_count, this.done_count);
            this.dialog.set_bottom_label("Done: " + this.done_count + " / " + this.file_count);
        }
        this.mpg.delete();
        this.mpg = null;
        this.dialog.close_and_run(null);
        this.player.reset_albums();
    }

    private void read_id3_tags(String filename, Song song, SongInfo obj) {
        song.filename = this.player.get_relative_filename(filename);
        song.add_time = (long)(System.currentTimeMillis() / 1000);
        var stats = new FileStats();
        if new File(filename).getStats(stats, true) != 0 {
            return;
        }
        song.mtime = (long)(stats.lastModified / 1000);

        var mpg = this.mpg;
        if mpg.open(filename) != 0 {
            return;
        }
        // Scan the file to calculate duration and bitrate.
        var fmt = mpg.get_format();
        var info = new FrameInfo();
        var total_bitrate = (long) 0;
        var frame_count = 0;
        while true {
            var status = mpg.framebyframe_next();
            if status != 0 && status != Mpg123.NEW_FORMAT {
                break;
            }
            mpg.info(info);
            total_bitrate = total_bitrate + info.bitrate;
            frame_count++;
        }
        if frame_count > 0 {
            song.duration = (int)(((double) mpg.tell()) / fmt.rate + 0.5);
            song.bitrate = info.vbr == Mpg123.CBR ? "" + info.bitrate :
                "~" + (int)(((double) total_bitrate) / frame_count);
        }
        var id3 = mpg.id3();
        mpg.close();

        song.id3 = id3.version;
        song.title = id3.title;
        obj.artist = id3.artist;
        obj.album = id3.album;
        obj.album_artist = id3.album_artist;
        song.track_number = id3.track_number;
        song.year = id3.year != null ? System.parseInt(id3.year) : 0;
        if id3.record_date != null && id3.record_date != "" {
            song.year = System.parseInt(id3.record_date);
        }
    }
}
