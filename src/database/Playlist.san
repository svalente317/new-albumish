/*
 *  Copyright (c) 2014  Salvatore Valente <svalente@mit.edu>
 *
 *  This program is free software.  You can modify and distribute it under
 *  the terms of the GNU General Public License.  There is no warranty.
 *  See the file "COPYING" for more information.
 */
package albumish.database;

/**
 * Playlist is simply a list of song IDs.
 */
class Playlist {
    String name;
    int id;

    private int[] song_list;
    private boolean is_changed;

    Playlist() {
        this.song_list = new int[];
    }

    /**
     * Get the list of songids in the playlist for read-only access.
     */
    int[] get_song_list() {
        return this.song_list;
    }

    /**
     * Add a song to the end of the playlist.
     */
    void add(int songid) {
        this.song_list.add(songid);
        this.is_changed = true;
    }

    /**
     * Get an item in the list relative to another item in the list.
     */
    int get_next_id(int songid, int delta) {
        var idx = -1;
        for var i = 0; i < this.song_list.length; i++ {
            if this.song_list[i] == songid {
                idx = i;
                break;
            }
        }
        if idx >= 0 {
            idx = idx + delta;
        }
        if idx < 0 || idx >= this.song_list.length {
            return 0;
        }
        return this.song_list[idx];
    }

    /**
     * Replace the playlist contents.
     */
    void reset(int[] new_song_list) {
        if new_song_list == null {
            this.song_list.setLength(0);
        } else {
            this.song_list.setLength(new_song_list.length);
            for var i = 0; i < new_song_list.length; i++ {
                this.song_list[i] = new_song_list[i];
            }
        }
    }
}
